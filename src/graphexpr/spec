// CUP specification for graph expressions

import java_cup.runtime.*;

import java.util.List;
import java.util.ArrayList;
import pregroup.SimpleType;
import pregroup.FreeType;

action code {:

public String name;

:}

/* Terminals */
terminal    NODE_FRESH, NODE, NAME, LPAREN, RPAREN, COMMA, SEP, LCRO, RCRO, PLUS, NULL, COLON;
terminal String IDENT, STR;
terminal Integer VAR, EXPR, EXPL;

/* Non-terminals */
non terminal List<GraphExpr>   expr_list;
non terminal PatternExpr pattern;
non terminal GraphExpr expr;
non terminal List<DoubleExpr> double_list;
non terminal NodeExpr node;
non terminal TripleExpr triple;
non terminal DoubleExpr double;
non terminal String string;
non terminal SimpleType<FreeType> stype;
non terminal Integer exp;

/* The grammar */
expr_list ::= expr_list:l SEP expr:e
            {: l.add(e);
               RESULT = l; :}
            | expr:e
            {: List<GraphExpr> l = new ArrayList<GraphExpr>();
               l.add(e);
               RESULT = l; :}
            ;

expr ::= stype:st pattern:p
        {: RESULT = new GraphExpr(st,p); :}
        ;

pattern ::= node:n
       {: RESULT = n; :}
       | triple:t
       {: RESULT = t; :}
       ;

node ::= VAR:v
       {: RESULT = new VarExpr(v); :}
       | NULL
       {: RESULT = new NullExpr(); :}
       | NODE LPAREN string:n RPAREN
       {: RESULT = new NamedNodeExpr(n); :}
       | NODE_FRESH
       {: RESULT = new FreshExpr(); :}
       | LCRO node:n PLUS double_list:l RCRO
       {: RESULT = new PlusExpr(n,l); :}
       ;
/* todo : add support for litterals */

double ::= LPAREN string:s COMMA node:n RPAREN
        {: RESULT = new DoubleExpr(s,n); :}
        ;

double_list ::= double_list:t PLUS double:e
            {: t.add(e);
               RESULT = t; :}
            | double:e
            {: List<DoubleExpr> l = new ArrayList();
               l.add(e);
               RESULT = l; :}
            ;

triple ::= LPAREN node:n COMMA string:p COMMA node:o RPAREN
           {: RESULT = new TripleExpr(n,p,o); :}
           ;

string ::= NAME
           {: RESULT = name; :}
         | STR:s
           {: RESULT = s; :}
           ;
/* todo : add support for litterals */

stype ::= IDENT:i COLON
          {: RESULT = new SimpleType<FreeType>(new FreeType(i,null),0); :}
        | IDENT:i LPAREN exp:e RPAREN COLON
          {: RESULT = new SimpleType<FreeType>(new FreeType(i,null),e); :}
        ;
/* TODO : fix design issue (comparison of types) */

exp ::= EXPL:e {: RESULT = -e; :}
      | EXPR:e {: RESULT = e; :};

